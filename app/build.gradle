apply plugin: 'com.android.application'

def app_android = rootProject.ext.androidID
def app_dependencies = rootProject.ext.dependenciesID

android {
    compileSdkVersion app_android.compileSdkVersion
    buildToolsVersion app_android.buildToolsVersion

    defaultConfig {
        applicationId app_android.applicationId
        minSdkVersion app_android.minSdkVersion
        targetSdkVersion app_android.targetSdkVersion
        versionCode app_android.versionCode
        versionName app_android.versionName

        testInstrumentationRunner app_android.testInstrumentationRunner
        //编译配置属性 在编译时生成java属性
        //切记：不能在android根节点，只能在defaultConfig或buildTypes节点下
        buildConfigField("boolean", "isRelease", String.valueOf(isRelease))
        //切记：必须写在defaultConfig节点下
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName       : project.getName(),
                             packageNameForAPT: packageNameForAPT]
            }
        }
    }
    //构建类型
    buildTypes {
        release {
            //todo 集成环境
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    app_dependencies.each { k, v -> implementation v }
    implementation project(":common")
    implementation project(":router_annotations")
    annotationProcessor project(':router_complier')
    if (isRelease) {
        implementation project(":order")
        implementation project(":personal")
    }
}